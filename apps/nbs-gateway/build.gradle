import org.springframework.boot.gradle.plugin.SpringBootPlugin

plugins {
    alias libs.plugins.springBoot
}

group = 'gov.cdc.nbs.gateway'
version = '0.0.1-SNAPSHOT'

springBoot {
    buildInfo {
        properties {
            additional = [
                    'commit': rootProject.ext.commit
            ]
        }
    }
}

bootJar {
    archiveFileName = "${archiveBaseName.get()}.${archiveExtension.get()}"
}

testing {
    suites {
        test {
            useJUnitJupiter()

            dependencies {
                implementation 'org.springframework.boot:spring-boot-starter-test'
                implementation libs.spring.security.test
                implementation "org.wiremock:wiremock-standalone:3.9.2"
            }
        }
    }
}

dependencies {

    implementation platform(SpringBootPlugin.BOM_COORDINATES)

    implementation libs.bundles.security
    implementation libs.bundles.oidc
    implementation 'org.springframework.cloud:spring-cloud-starter-gateway:4.2.0'
    implementation 'org.springframework.security:spring-security-oauth2-client'
    implementation 'org.springframework.security:spring-security-oauth2-jose'

}

tasks.withType(JavaExec).configureEach {

    debugOptions {
        enabled = true
        server = true
        suspend = false
        port = java.util.Optional.ofNullable(findProperty('debug.port'))
                .map(Object::toString)
                .map(Integer::parseInt)
                .orElse(5008)
    }
}

tasks.named("bootRun") {
    // Enables the local profile when run from the command line
    systemProperty 'spring.profiles.active', 'default,development,local'
}
