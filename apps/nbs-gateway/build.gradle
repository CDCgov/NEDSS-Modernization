import org.springframework.boot.gradle.plugin.SpringBootPlugin

plugins {
    id 'jvm-test-suite'
    id 'org.springframework.boot' version '3.1.5'
}

apply plugin: 'io.spring.dependency-management'

group = 'gov.cdc.nbs.gateway'
version = '0.0.1-SNAPSHOT'

bootJar {
    manifest {
        attributes 'API-Version': archiveVersion
    }

    archiveFileName = "${archiveBaseName.get()}.${archiveExtension.get()}"
}

testing {
    suites {
        test {
            useJUnitJupiter()

            dependencies {
                implementation 'org.springframework.boot:spring-boot-starter-test'
                implementation "org.wiremock:wiremock:3.1.0"
            }
        }
    }
}

dependencies {

    implementation platform(SpringBootPlugin.BOM_COORDINATES)
    implementation platform("org.springframework.cloud:spring-cloud-dependencies:2022.0.4")
    implementation libs.snake.yaml
    implementation 'org.springframework.cloud:spring-cloud-starter-gateway'
}

tasks.withType(JavaExec).configureEach {

    debugOptions {
        enabled = true
        server = true
        suspend = false
        port = java.util.Optional.ofNullable(findProperty('debug.port'))
                .map(Object::toString)
                .map(Integer::parseInt)
                .orElse(5008)
    }
}

tasks.named("bootRun") {
    // Enables the local profile when run from the command line
    systemProperty 'spring.profiles.active', 'default,development'
}
