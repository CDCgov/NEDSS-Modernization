import org.springframework.boot.gradle.plugin.SpringBootPlugin

plugins {
    id 'jvm-test-suite'

    alias libs.plugins.springBoot
    alias libs.plugins.springDependencies
}

group = 'gov.cdc.nbs'
version = '0.0.1-SNAPSHOT'

testing {
    suites {
        test {
            useJUnitJupiter()

            dependencies {
                implementation libs.bundles.testing
                implementation libs.spring.test
                implementation libs.spring.kafka.test

                compileOnly libs.lombok
                annotationProcessor libs.lombok

                implementation libs.bundles.testcontainers.elasticsearch

                implementation libs.bundles.javafaker

            }
        }
    }
}

dependencies {

    implementation platform(SpringBootPlugin.BOM_COORDINATES)

    implementation project(':database-entities')
    implementation project(':event-schema')
    implementation project(':authentication')

    // Spring
    implementation libs.bundles.jpa
    annotationProcessor libs.springBoot.jpa

    implementation libs.bundles.security

    implementation libs.spring.json

    // Kafka
    implementation libs.spring.kafka


    // lombok
    compileOnly libs.lombok
    annotationProcessor libs.lombok

    // database
    implementation libs.mssql.jdbc

	// querydsl
    implementation libs.bundles.queryDSL
    annotationProcessor(variantOf(libs.queryDSL.apt) { classifier('jpa') })

    implementation libs.bundles.jackson

    // elasticsearch
    implementation libs.bundles.elasticsearch

}

compileJava.mustRunAfter(":database-entities:build")
compileJava.mustRunAfter(":event-schema:build")
compileJava.mustRunAfter(":authentication:build")
