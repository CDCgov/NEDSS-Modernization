import org.springframework.boot.gradle.plugin.SpringBootPlugin

plugins {
    id 'jvm-test-suite'
    alias libs.plugins.springBoot
    alias libs.plugins.springDependencies
}

group = 'gov.cdc.nbs.page.builder'
version = '0.0.1-SNAPSHOT'

apply from: "${rootDir}/gradle/cucumber-logging.gradle"

bootJar {
    manifest {
        attributes 'API-Version': archiveVersion
    }

    archiveFileName = "${archiveBaseName.get()}.${archiveExtension.get()}"
}

test {
    // always re-run tests
    test.outputs.upToDateWhen { false }
    
    systemProperty "cucumber.filter.tags", System.getProperty("cucumber.filter.tags")

    // support Github actions overriding the configured database image
    if (System.getProperty("testing.database.image") != null) {
        systemProperty "testing.database.image", System.getProperty("testing.database.image")
    }

        debugOptions {
        enabled = true
        server = true
        suspend = false
        port = java.util.Optional.ofNullable(findProperty('debug.port'))
                .map(Object::toString)
                .map(Integer::parseInt)
                .orElse(5105)
    }
}

testing {
    suites {
        test {
            useJUnitJupiter()

            dependencies {
                compileOnly libs.lombok
                annotationProcessor libs.lombok

                implementation libs.bundles.testing
                implementation libs.spring.test
                implementation libs.spring.kafka.test
                implementation 'org.awaitility:awaitility:4.2.0'

                implementation libs.bundles.testcontainers

                implementation libs.bundles.cucumber

                implementation project(':testing:support')
                implementation project(':testing:identity')
                implementation project(':testing:auth')
                implementation project(':testing:auth-cucumber')
                implementation project(':testing:http-interaction')
            }
        }
    }
}

dependencies {
    implementation platform(SpringBootPlugin.BOM_COORDINATES)

    implementation project(':authentication')
    implementation project(':audit')
    implementation project(':id-generator')

    implementation libs.snake.yaml
    implementation libs.guava

    implementation libs.springBoot.web
    implementation libs.bundles.jpa
    annotationProcessor libs.springBoot.jpa

    implementation libs.bundles.security

    // swagger
    implementation libs.bundles.swagger

    // querydsl
    implementation libs.bundles.queryDSL
    annotationProcessor(variantOf(libs.queryDSL.apt) { classifier('jpa') })

    // blazebit for querydsl paging
    implementation libs.bundles.blaze.persistence.queryDSL
    implementation libs.bundles.jackson

    // database
    implementation libs.mssql.jdbc

    // Kafka
    implementation libs.spring.kafka

    // lombok
    compileOnly libs.lombok
    annotationProcessor libs.lombok
    
    
    // csv
    implementation "org.apache.commons:commons-csv:1.9.0"

    // itext pdf generation
    implementation libs.itextpdf

}

compileJava {
    options.compilerArgs << '-Aquerydsl.generatedAnnotationClass=com.querydsl.core.annotations.Generated'
}

tasks.withType(JavaExec).configureEach {

    debugOptions {
        enabled = true
        server = true
        suspend = false
        port = java.util.Optional.ofNullable(findProperty('debug.port'))
                .map(Object::toString)
                .map(Integer::parseInt)
                .orElse(18095)
    }
}
