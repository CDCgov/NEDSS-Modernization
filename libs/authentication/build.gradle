import org.springframework.boot.gradle.plugin.SpringBootPlugin

plugins {
    id 'jvm-test-suite'

    alias libs.plugins.springBoot apply false
    alias libs.plugins.springDependencies
}



testing {
    suites {
        test {
            useJUnitJupiter()

            dependencies {
                implementation libs.bundles.testing
                implementation libs.spring.test
                implementation libs.spring.kafka.test
            }
        }
    }
}

dependencyManagement {
    imports {
        mavenBom SpringBootPlugin.BOM_COORDINATES
    }
}
dependencies {
    implementation project(':accumulation')

    // core
    implementation libs.bundles.jpa
    annotationProcessor libs.springBoot.jpa

    implementation libs.springBoot.web
    implementation libs.bundles.security

    // database
    implementation libs.mssql.jdbc

    // querydsl
    implementation libs.bundles.queryDSL
    annotationProcessor(variantOf(libs.queryDSL.apt) { classifier('jpa') })

    // blazebit for querydsl paging
    implementation libs.bundles.blaze.persistence.queryDSL
    implementation libs.bundles.jackson

    // lombok
    compileOnly libs.lombok
    annotationProcessor libs.lombok

}

compileJava {
    options.compilerArgs << '-Aquerydsl.generatedAnnotationClass=com.querydsl.core.annotations.Generated'
}
