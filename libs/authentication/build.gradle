import org.springframework.boot.gradle.plugin.SpringBootPlugin

plugins {
    id 'jvm-test-suite'

    alias libs.plugins.springBoot apply false
    alias libs.plugins.springDependencies
}

group = 'gov.cdc'
version = '1.0.0-SNAPSHOT'

testing {
    suites {
        test {
            useJUnitJupiter()

            dependencies {
                implementation libs.bundles.testing
                implementation libs.spring.kafka.test
            }
        }
    }
}

dependencyManagement {
    imports {
        mavenBom SpringBootPlugin.BOM_COORDINATES
    }
}
dependencies {
    implementation project(':database-entities')

    // core
    implementation libs.bundles.jpa
    annotationProcessor libs.springBoot.jpa

    implementation libs.bundles.security

    // database
    implementation libs.mssql.jdbc

    // querydsl
    implementation libs.bundles.queryDSL
    annotationProcessor(variantOf(libs.queryDSL.apt) { classifier('jpa') })

    // blazebit for querydsl paging
    implementation libs.bundles.blaze.persistence.queryDSL
    implementation libs.bundles.jackson

    // lombok
    compileOnly libs.lombok
    annotationProcessor libs.lombok

    // Kafka
    implementation libs.spring.kafka
    
    // swagger
    implementation "io.springfox:springfox-boot-starter:3.0.0"
}
