import org.springframework.boot.gradle.plugin.SpringBootPlugin

plugins {
    id 'jvm-test-suite'

    alias libs.plugins.springBoot apply false
    alias libs.plugins.springDependencies
}

group = 'gov.cdc'
version = '1.0.0-SNAPSHOT'



testing {
    suites {
        test {
            useJUnitJupiter()

            dependencies {
                implementation libs.bundles.testing
            }
        }
    }
}

dependencyManagement {
    imports {
        mavenBom SpringBootPlugin.BOM_COORDINATES
    }
}

dependencies {

    implementation project(':event-schema')

    // Spring
    implementation libs.bundles.jpa
    annotationProcessor libs.springBoot.jpa

    implementation libs.bundles.elasticsearch

    // lombok
    compileOnly libs.lombok
    annotationProcessor libs.lombok

    // querydsl
    implementation(variantOf(libs.queryDSL.jpa) { classifier("jakarta") })
    annotationProcessor(variantOf(libs.queryDSL.apt) { classifier("jakarta") })

}

compileJava {
    options.compilerArgs << '-Aquerydsl.generatedAnnotationClass=com.querydsl.core.annotations.Generated'
}
