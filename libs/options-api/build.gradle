import org.springframework.boot.gradle.plugin.SpringBootPlugin

plugins {
    id 'jvm-test-suite'

    alias libs.plugins.springBoot apply false
    alias libs.plugins.springDependencies
}

apply from: "${rootDir}/gradle/cucumber-logging.gradle"

group = 'gov.cdc.nbs.options'

testing {
    suites {
        test {
            useJUnitJupiter()
            dependencies {
                implementation libs.bundles.testing
                implementation libs.spring.test

                implementation libs.mssql.jdbc
                implementation libs.bundles.testcontainers
                implementation libs.bundles.cucumber

                implementation project(':testing:support')
                implementation project(':testing:database')
            }
        }
    }
}

dependencies {
    implementation platform(SpringBootPlugin.BOM_COORDINATES)

    implementation libs.springBoot.web
    implementation libs.bundles.swagger
    implementation libs.springBoot.jdbc
}

test {
    if (System.getProperty("testing.database.image") != null) {
        systemProperty "testing.database.image", System.getProperty("testing.database.image")
    }
}

