plugins {
    alias libs.plugins.springBoot apply false
}

apply from: "${rootDir}/gradle/cucumber-logging.gradle"

group = 'gov.cdc.nbs.options'

testing {
    suites {
        test {
            useJUnitJupiter()
            dependencies {
                implementation.bundle(libs.bundles.testing)
                implementation libs.spring.test

                implementation libs.mssql.jdbc
                implementation.bundle(libs.bundles.testcontainers)
                implementation.bundle(libs.bundles.cucumber)

                implementation project(':testing-support')
                implementation project(':testing-database')
            }
        }
    }
}

dependencies {
    implementation libs.guava
    implementation libs.springBoot.web
    implementation libs.springBoot.jdbc
    implementation libs.bundles.swagger
    implementation libs.bundles.jpa
    annotationProcessor libs.springBoot.jpa
    implementation(project(':database-entities'))
    implementation(variantOf(libs.queryDSL.jpa) { classifier("jakarta") })
    annotationProcessor(variantOf(libs.queryDSL.apt) { classifier("jakarta") })
}

compileJava {
    options.compilerArgs << '-Aquerydsl.generatedAnnotationClass=com.querydsl.core.annotations.Generated'
}

test {
    if (System.getProperty("testing.database.image") != null) {
        systemProperty "testing.database.image", System.getProperty("testing.database.image")
    }
}

