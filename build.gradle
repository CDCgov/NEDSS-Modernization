buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }

    dependencies {
        classpath "com.moowork.gradle:gradle-node-plugin:1.3.1"
        classpath ("com.dipien:semantic-version-gradle-plugin:2.0.0")
    }
}

plugins {
    id "org.sonarqube" version "3.5.0.2730"
}

version = '1.0.0-SNAPSHOT'  // The plugin will assign the root project version to all its subprojects.
apply plugin: "com.dipien.semantic-version"
def cucumberScriptPath = file("./gradle/cucumber-logging.gradle")

subprojects {

    repositories {
        mavenCentral()
        maven {
            url "https://packages.confluent.io/maven"
        }
    }

    if (!name.contains("-ui")) {

        apply plugin: 'java'
        apply plugin: 'jacoco'

        // Displays cucumber test results
        apply from: cucumberScriptPath

        jacocoTestReport {
            dependsOn test
            reports {
                xml.required = true
            }
        }

        tasks.named('test') {
            finalizedBy tasks.named('jacocoTestReport', JacocoReport)
        }

    }
}

sonarqube {
    properties {
        property "sonar.sourceEncoding", "UTF-8"
        property "sonar.projectKey", "CDCgov_NEDSS-Modernization"
        property "sonar.organization", "cdcgov"
        property "sonar.host.url", "https://sonarcloud.io"
        property "sonar.coverage.jacoco.xmlReportPaths", "${rootDir}/apps/modernization-api/build/reports/jacoco/test/jacocoTestReport.xml,${rootDir}/coverage-report/build/jacoco/testCodeCoverageReport/testCodeCoverageReport.xml"
    }
}
