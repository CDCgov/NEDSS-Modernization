buildscript {
    repositories {
        maven {
            url = 'https://plugins.gradle.org/m2/'
        }
    }

    dependencies {
        classpath('com.dipien:semantic-version-gradle-plugin:2.0.0')
    }

    ext {
        commit = System.getenv('GITHUB_SHA') ?: ''
    }
}

plugins {
    id 'java-library'
    id 'jacoco'
    id 'org.sonarqube' version '6.2.0.5505'
    id 'com.diffplug.spotless'  version '8.2.1'
}

version = '1.0.1-SNAPSHOT'  // The plugin will assign the root project version to all its subprojects.
apply plugin: 'com.dipien.semantic-version'
apply from: "${rootDir}/gradle/sonar.gradle"

subprojects {
    repositories {
        mavenCentral()
        maven {
            url = 'https://packages.confluent.io/maven'
        }
    }

    apply plugin: 'java'
    apply plugin: 'jvm-test-suite'
    apply plugin: 'jacoco'

    // run spotless on build if git present (not in a docker container)
    def gitFolder = new File( '.git' )
    if (gitFolder.exists()) {
        apply plugin: 'com.diffplug.spotless'

        spotless {
            // limit format enforcement to just the files changed by this feature branch
            ratchetFrom 'origin/main'

            java {
                targetExclude '**/generated/**/*.java'
                googleJavaFormat()
                formatAnnotations()
                trimTrailingWhitespace()
                endWithNewline()
            }
        }

    
        tasks.named('compileJava') {
            dependsOn tasks.named('spotlessApply')
        }
    }

    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(21)
            vendor = JvmVendorSpec.ADOPTIUM
        }

    }

    tasks.withType(JavaCompile).tap {
        configureEach {
            options.compilerArgs << '-parameters'
        }
    }

}
