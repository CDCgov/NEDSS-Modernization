import org.gradle.api.tasks.testing.logging.TestExceptionFormat
import org.gradle.api.tasks.testing.logging.TestLogEvent
import groovy.time.TimeCategory

ext.testsResults = [] // Container for tests summaries

tasks.named("test") { testTask ->
    testsResults = []
    testLogging {
        events "passed", "failed"
        exceptionFormat TestExceptionFormat.FULL
        showExceptions true
        showCauses true
        showStackTraces true
    }

    ignoreFailures = true // Always try to run all tests for all modules
    afterSuite { desc, result ->

        if (desc.parent) return // Only summarize results for whole modules
        if (desc.getParent() == null) {
            rootProject.ext.testFailures += result.getFailedTestCount() //count failures
        }

        String summary = "${testTask.project.name}:${testTask.name} results: ${result.resultType} " +
                "(" +
                "${result.testCount} tests, " +
                "${result.successfulTestCount} successes, " +
                "${result.failedTestCount} failures, " +
                "${result.skippedTestCount} skipped" +
                ") " +
                "in ${TimeCategory.minus(new Date(result.endTime), new Date(result.startTime))}" +
                "\n" +
                "Report file: ${testTask.reports.html.entryPoint}" + "\n" +
                "Cucumber report: ${project.projectDir}/build/reports/tests/test/cucumber-report.html" + "\n" +
                "Jacoco report: ${project.projectDir}/build/reports/jacoco/test/html/index.html"


        // Add reports in `testsResults`, keep failed suites at the end
        if (result.resultType == TestResult.ResultType.SUCCESS) {
            testsResults.add(0, summary)
        } else {
            testsResults += summary
        }
    }
}


gradle.buildFinished {
    def allResults = ext.testsResults

    if (!allResults.isEmpty()) {
        printResults allResults
    }
}

private static void printResults(allResults) {
    def maxLength = allResults*.readLines().flatten().collect { it.length() }.max()

    println "┌${"${"─" * maxLength}"}┐"

    println allResults.collect {
        it.readLines().collect {
            "│" + it + " " * (maxLength - it.length()) + "│"
        }.join("\n")
    }.join("\n├${"${"─" * maxLength}"}┤\n")

    println "└${"${"─" * maxLength}"}┘"
}
